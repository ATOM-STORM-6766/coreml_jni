apply plugin: 'maven-publish'

def nativeName = wpilibTools.platformMapper.currentPlatform.platformName;
if (nativeName.equals("macx64") || nativeName.equals("macarm64")) {
    nativeName = "osxuniversal";
}

def artifactGroupId = 'org.atomstorm'
def baseArtifactId = "coreml_jni"

def jniType = project.findProperty("PublishType") ?: "";
def finalVersion = pubVersion + (jniType.isEmpty() ? "" : "-${jniType}")
println("Building with JNI publish type: " + jniType);

task nativeLibraryJar(type: Jar, dependsOn: copyNativeLibrary) {
    archiveClassifier = nativeName
    from outputsFolder
}
build.dependsOn nativeLibraryJar

publishing {
    repositories {
        maven {
            url ('https://maven.cloudsmith.io/atomstorm/coreml-jni' + (isDev ? '-snapshots' : ''))
            credentials {
                username System.getenv("ARTIFACTORY_USERNAME")
                password System.getenv("ARTIFACTORY_API_KEY")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = artifactGroupId
            artifactId = "${baseArtifactId}-java"
            version = finalVersion;

            from components.java
        }
        mavenJNI(MavenPublication) {
            groupId = artifactGroupId
            artifactId = "${baseArtifactId}-jni"
            version = finalVersion;

            artifact nativeLibraryJar
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    doFirst {
        println("Publishing coreml-jni to " + repository.url)
    }
}

